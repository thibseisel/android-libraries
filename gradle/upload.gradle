/*
 * Copyright 2018 Thibault Seisel
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Include this script in an Android Library Project to use the "bintrayUpload" task
 * to upload artifacts to Bintray JCenter.
 *
 * Required parameters :
 * * libGroup - The group id of the library
 * * libName - The artifact id of the library
 * * libVersion - The version of the library
 * * libDescription - A description of the library's main features.
 */

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group libGroup
version libVersion

def signing = new Properties()
signing.load(file("$rootDir/signing.properties").newReader())

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name libName
            description libDescription
            inceptionYear '2018'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Thibault Seisel'
                }
            }
        }
    }
}

bintray {
    user = signing.getProperty('bintray.user')
    key = signing.getProperty('bintray.apikey')
    configurations = ['archives']

    pkg {
        repo = 'android'
        name = libName
        publish = false
        override = true

        version {
            name = libVersion
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from fileTree(new File(project.buildDir, 'javadoc'))
}

artifacts {
    archives javadocJar
    archives sourcesJar
}